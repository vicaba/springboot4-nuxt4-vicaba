import com.github.gradle.node.npm.task.NpmSetupTask
import com.github.gradle.node.npm.task.NpmTask
import com.github.gradle.node.task.NodeSetupTask
import org.gradle.api.plugins.jvm.JvmTestSuite
import org.springframework.boot.gradle.plugin.SpringBootPlugin
import org.springframework.boot.gradle.tasks.bundling.BootJar

plugins {
    alias(libs.plugins.kotlin.jvm)
    alias(libs.plugins.kotlin.spring)
    alias(libs.plugins.spring.boot)
    alias(libs.plugins.gradle.node)
    alias(libs.plugins.version.catalog.update)
    alias(libs.plugins.spotless)
    alias(libs.plugins.openapi.generator)
}

group = "com.example"
version = "0.0.1-SNAPSHOT"

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

kotlin {
    compilerOptions {
        freeCompilerArgs.addAll("-Xjsr305=strict")
    }
}

sourceSets {
    main {
        kotlin {
            srcDir(layout.buildDirectory.dir("generated/openapi/src/main/kotlin"))
        }
    }
}

configurations {
    compileOnly {
        extendsFrom(configurations.annotationProcessor.get())
    }
}

dependencies {
    val bom = platform(SpringBootPlugin.BOM_COORDINATES)
    implementation(bom)
    developmentOnly(bom)
    annotationProcessor(bom)

    implementation(libs.jackson.module.kotlin)
    implementation(libs.kotlin.reflect)
    implementation(libs.spring.boot.starter.actuator)
    implementation(libs.spring.boot.starter.web)
    implementation(libs.swagger.annotations)
    implementation(libs.jakarta.validation.api)

    developmentOnly(libs.spring.boot.devtools)

    annotationProcessor(libs.spring.boot.configuration.processor)
}

openApiGenerate {
    generatorName.set("kotlin-spring")
    inputSpec.set("$rootDir/openapi/api.yaml")
    outputDir.set(
        layout.buildDirectory
            .dir("generated/openapi")
            .get()
            .asFile.path,
    )
    apiPackage.set("com.example.demo.api")
    modelPackage.set("com.example.demo.model")
    configOptions.set(
        mapOf(
            "interfaceOnly" to "true",
            "useSpringBoot3" to "true",
            "useTags" to "true",
            "documentationProvider" to "none",
            "enumPropertyNaming" to "UPPERCASE",
            "serializationLibrary" to "jackson",
        ),
    )
}

tasks.named("compileKotlin") {
    dependsOn(tasks.named("openApiGenerate"))
}

testing {
    suites {
        withType<JvmTestSuite>().configureEach {
            useJUnitJupiter()

            dependencies {
                implementation(platform(SpringBootPlugin.BOM_COORDINATES))
                implementation(platform(libs.kotest.bom))
                implementation(libs.kotest.assertions.core)
                implementation(libs.kotest.runner.junit5)
                implementation(libs.mockk)
            }
        }

        val integrationTest by registering(JvmTestSuite::class) {
            sources {
                kotlin {
                    setSrcDirs(listOf("src/it/kotlin"))
                }
                resources {
                    setSrcDirs(listOf("src/it/resources"))
                }
            }

            dependencies {
                implementation(project())
                implementation(libs.kotest.extensions.spring)
                implementation(libs.spring.boot.starter.test)
                implementation(libs.spring.boot.starter.web)
            }

            targets.all {
                testTask.configure {
                    shouldRunAfter(tasks.named("test"))
                }
            }
        }
    }
}

tasks.withType<BootJar>().configureEach {
    archiveFileName.set("app.jar")
}

tasks.named("check") {
    dependsOn(testing.suites.named("integrationTest"))
}

node {
    version = "24.14.0"
    download = true
}

val frontendDir = layout.projectDirectory.dir("frontend")

val frontendInstall by tasks.registering(NpmTask::class) {
    workingDir = frontendDir.asFile
    args = listOf("ci")
    inputs.file(frontendDir.file("package-lock.json"))
    outputs.dir(frontendDir.dir("node_modules"))
}

val frontendGenerate by tasks.registering(NpmTask::class) {
    workingDir = frontendDir.asFile
    args = listOf("run", "generate")
    dependsOn(frontendInstall)
    inputs.dir(frontendDir.dir("app"))
    inputs.file(frontendDir.file("nuxt.config.ts"))
    inputs.file(frontendDir.file("package.json"))
    outputs.dir(frontendDir.dir(".output/public"))
}

val copyFrontend by tasks.registering(Copy::class) {
    from(frontendDir.dir(".output/public"))
    into(layout.buildDirectory.dir("resources/main/static"))
    dependsOn(frontendGenerate)
}

tasks.named("processResources") {
    dependsOn(copyFrontend)
}

spotless {
    val isWindows = System.getProperty("os.name").lowercase().contains("windows")
    val executable: (String, String) -> String = { win, unix -> if (isWindows) win else "bin/$unix" }
    val nodeExecutable by lazy { "${tasks.named<NodeSetupTask>("nodeSetup").get().nodeDir.get()}/${executable("node.exe", "node")}" }
    val npmExecutable by lazy { "${tasks.named<NpmSetupTask>("npmSetup").get().npmDir.get()}/${executable("npm.cmd", "npm")}" }
    val prettier = "prettier" to "3.8.1"
    val prettierPluginSh = "prettier-plugin-sh" to "0.18.0"
    val defaultTargetExcludes =
        listOf(
            "**/.git/**",
            "**/.gradle/**",
            "**/.idea/**",
            "**/.claude/**",
            "**/.output/**",
            "**/bin/**",
            "**/build/**",
            "**/gradle/**",
            "**/frontend/**",
            "frontend/**",
            "**/src/main/resources/static/**",
        )

    kotlin {
        target("**/*.kt")
        targetExclude(defaultTargetExcludes)
        toggleOffOn()
        ktlint()
    }
    kotlinGradle {
        target("**/*.gradle.kts")
        targetExclude(defaultTargetExcludes)
        toggleOffOn()
        ktlint()
    }
    format("prettier") {
        target("**/*.json", "**/*.js", "**/*.md", "**/*.yml", "**/*.yaml")
        targetExclude(defaultTargetExcludes)
        prettier(mapOf(prettier))
            .nodeExecutable(nodeExecutable)
            .npmExecutable(npmExecutable)
    }
    format("sh") {
        target("**/Dockerfile", "**/*.env", "**/.gitignore", "**/*.sh")
        targetExclude(defaultTargetExcludes)
        prettier(mapOf(prettier, prettierPluginSh))
            .nodeExecutable(nodeExecutable)
            .npmExecutable(npmExecutable)
            .config(
                mapOf(
                    "plugins" to listOf("prettier-plugin-sh"),
                    "indent" to 4,
                ),
            )
    }

    listOf("spotlessPrettier", "spotlessSh").forEach { taskName ->
        tasks.named(taskName) {
            dependsOn(tasks.named("nodeSetup"))
            mustRunAfter(frontendInstall)
        }
    }
}
